\documentclass[titlepage]{jsarticle}

\usepackage{color}
\usepackage[dvips]{graphicx}
\usepackage{listings}

\title{コンピュータグラフィックス基礎 課題3}
\author{201111365 坂口和彦}

\lstdefinestyle{program}{
  basicstyle=\small\tt,
  keywordstyle=\bf,
  identifierstyle=,
  commentstyle=,
  stringstyle=,
  emphstyle=,
  backgroundcolor=\color[gray]{0.8},
  language=Haskell,
  frame=trbl,
  rulecolor=\color{white},
  numbers=left,
  numberstyle=,
  basewidth={0.54em, 0.45em},
  lineskip=-0.2zw
}

\renewcommand{\lstlistingname}{リスト}

\begin{document}

\maketitle

\section{課題}

私は、300個の四角形がそれぞれランダムな色や位置を持ち、回転と移動を一定のスピードでするプログラムを記述した。
このプログラムはプログラミング言語HaskellとGLUTライブバリによって記述されている。ソースコードをリスト\ref{list:impl1}に示す。

\begin{lstlisting}[style=program, label=list:impl1, caption=課題3のソースコード]
module Main where

import Data.IORef
import Control.Monad
import Control.Applicative
import System.Random
import Graphics.UI.GLUT

vertex3f :: Vertex3 GLfloat -> IO ()
vertex3f = vertex

type ObjState = 
    (Color4 GLfloat, -- color
     (GLfloat, GLfloat), -- rotate
     (Vector3 GLfloat, Vector3 GLfloat)) -- translate

vec3add :: Num a => Vector3 a -> Vector3 a -> Vector3 a
vec3add (Vector3 a b c) (Vector3 a' b' c') = Vector3 (a + a') (b + b') (c + c')

newObj :: IO ObjState
newObj = do
    let rand = randomRIO (0, 1)
        rand' = randomRIO (-0.001, 0.001)
    c <- liftM4 Color4 rand rand rand rand
    r <- liftM2 (,) (randomRIO (-10, 10)) (randomRIO (0, 360))
    t <- liftM2 (,)
        (liftM3 Vector3 rand' rand' rand')
        (liftM3 Vector3 rand rand rand)
    return (c, r, t)

step :: IORef [ObjState] -> IO ()
step state = do
    state' <- map (\(c, (r, r'), (t, t')) ->
        (c, (r, r + r'), (t, vec3add t t'))) <$> readIORef state
    case state' of
        [] -> writeIORef state []
        (_ : state') -> do
            obj <- newObj
            writeIORef state $ state' ++ [obj]

display :: IORef [ObjState] -> IO ()
display state = do
    clear [ColorBuffer]
    state' <- readIORef state
    forM_ state' $ \(c, (r, r'), (t, t')) -> preservingMatrix $ do
        color c
        translate t'
        rotate r' (Vector3 0 0 1)
        renderPrimitive Polygon $ mapM_ vertex3f
            [Vertex3 (-0.03) (-0.03) 0, Vertex3 0.03 (-0.03) 0,
             Vertex3 0.03 0.03 0, Vertex3 (-0.03) 0.03 0]
    flush

myInit :: IO ()
myInit = do
    clearColor $= Color4 0 0 0 0
    matrixMode $= Projection
    loadIdentity
    ortho 0 1 0 1 (-1) 1

makeTimer :: Int -> IO () -> IO ()
makeTimer time act = addTimerCallback time $ act >> makeTimer time act

main :: IO ()
main = do
    getArgsAndInitialize
    initialDisplayMode $= [ SingleBuffered, RGBMode ]
    initialWindowSize $= Size 250 250
    initialWindowPosition $= Position 100 100
    createWindow "hello"
    myInit
    state <- replicateM 300 newObj >>= newIORef
    displayCallback $= display state
    makeTimer 5 (step state >> display state)
    mainLoop
\end{lstlisting}

このプログラムを実行するには、Haskell Platformをインストールした上でrunhaskellコマンドを用いれば良い。

このプログラムの実行結果を図\ref{fig:output1}に示す。

\begin{figure}[htbp]
\begin{center}
\includegraphics[width = 150mm]{1.ps}
\end{center}
\caption{課題3-1の実行結果}
\label{fig:output1}
\end{figure}

この図だけでは分からないが、実行してみるとそれぞれの四角形が別々に動いていることが分かる。
それぞれの四角形は、回転の行列を掛けた上で移動させている。

\section{工夫/苦労した点、感想等}

HaskellのGLUTライブラリに関する資料があまり充実していなかったため、GLUTライブラリの実装を読む必要があった。

ある程度使えるようになったため、今後も使っていきたい。

\end{document}

